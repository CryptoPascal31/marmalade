(begin-tx)
(env-data
 { 'ns-admin-keyset: ["admin"]
 , 'ns-operate-keyset: ["operate"]
 , 'ns-genesis-keyset: { "keys": [], "pred": "="}
 , 'util-ns-admin: []
 , 'hft-admin: []
 , 'ns: "user"
 , 'upgrade: false })
(load "root/fungible-v2.pact")
(load "root/ns.pact")
(load "account-protocols-v1.pact")
(load "fungible-util.pact")
(commit-tx)

(begin-tx)
(load "hft.pact")
(verify "user.hft")
(commit-tx)

(begin-tx)
(env-data
 { 'bob-ks: ["bob"],
   'alice-ks: ["alice"],
   'ted-ks: ["ted"],
   'multi-ks: ["alice", "bob"]
 })
(env-keys ["bob"])
(use user.hft)

(env-sigs [
  { 'key: "bob"
  , 'caps:
    [
     (MINT "project-0" "bob" 5.0),
     (TRANSFER "project-0" "bob" "alice" 2.0),
     (TRANSFER "project-0" "bob" "project-0:1" 1.0),
     (TRANSFER "project-0:1" "bob" "alice"  0.000000002),
     (MINT "project-0:1" "bob" 1.0),
     (TRANSFER "project-0" "bob" "k:alice" 2.0)
    ]
  },
  {
    'key: "project-0:1",
    'caps: [
     (MINT "project-0:1" "bob" 1.0)
    ]
  },
  {
    'key: "alice",
    'caps: [
      (TRANSFER "project-0:1" "alice" "project-0:1:1" 1.0)
      (MINT "project-0" "alice" 5.0)
    ]
  }
  ])


(expect
  "bob creates a new token project-0"
  "Write succeeded"
  (create-token "project-0" (read-keyset 'bob-ks) 12 "project-0-uri"))

(expect
  "bob mints a 5.0 of token"
  "Write succeeded"
  (mint "project-0" "bob" (read-keyset 'bob-ks) 5.0))

(expect
  "total supply of project-0"
  5.0
  (total-supply "project-0"))

(expect-failure
  "alice can't mint a project-0 token"
  (mint "project-0" "alice" (read-keyset 'alice-ks) 5.0))

;;regular transfer
(expect "Bob transfers 2.0 to Alice"
  "Write succeeded"
  (transfer-create "project-0" "bob" "alice" (read-keyset 'alice-ks) 2.0))

(commit-tx)


(begin-tx)
;;Single key account protcol - transfer-create
(use user.hft)
(expect-failure "Bob transfers 2.0 to k:alice with guard multi-ks"
  "Single-key account protocol violation"
  (transfer-create "project-0" "bob" "k:alice" (read-keyset 'multi-ks) 1.0))

(expect-failure "Bob transfers 2.0 to k:alice with a single key keyset ted-ks"
  "Single-key account protocol violation"
  (transfer-create "project-0" "bob" "k:alice" (read-keyset 'ted-ks) 1.0))

(expect "Bob transfers 2.0 to k:alice with a single key keyset alice-ks"
  "Write succeeded"
  (transfer-create "project-0" "bob" "k:alice" (read-keyset 'alice-ks) 1.0))

;;Single key account protcol - Create Account
(expect-failure "Create account k:alice with multi-keyset"
  "Single-key account protocol violation"
  (create-account "project-0" "k:alice" (read-keyset 'multi-ks) ))

(expect-failure "Create account k:alice with a single key keyset ted-ks"
  "Single-key account protocol violation"
  (create-account "project-0" "k:alice" (read-keyset 'ted-ks)))

(expect "Create account k:alice with a single key keyset alice-ks"
  "Write succeeded"
  (create-account "project-0" "k:alice" (read-keyset 'alice-ks)))
(rollback-tx)

(begin-tx)
(use user.hft)
(view-tokens)
(view-ledger)
(commit-tx)
